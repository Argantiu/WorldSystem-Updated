plugins {
    id 'com.gradleup.shadow' version '8.3.6'
    id 'io.freefair.lombok' version '8.12'
    id 'java'
    id 'jacoco'
    id 'base'
    id 'maven-publish'
}

java { 
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
base { 
    archivesName = "${project.pluginname}" 
}

ext { // Dependency versions
    SpigotAPI = "1.21.3-R0.1-SNAPSHOT"
    AuthLib = "1.5.26"
    WorldEdit = "7.3.6-SNAPSHOT"
    FAWE = "2.12.3"
    VaultAPI = "1.7.1"
    PlaceholderAPI = "2.11.6"
    Lombok = "1.18.34"
    CommonsIO = "2.18.0"
    Minimessage = "4.18.0"
    SnakeYAML = "2.3"
    // Test dependencies
    Jupiter= "5.+"
    Mockito= "5.+"
    Assertj= "3.+"

}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    // Plugin repositories
    maven { url "https://repo.extendedclip.com/releases/" }
    maven { url "https://maven.enginehub.org/repo/" }
}

dependencies {
    // YAML Parser
    implementation("org.yaml:snakeyaml:${SnakeYAML}")
    
    // Adventure API for text formatting
    implementation("net.kyori:adventure-text-minimessage:${Minimessage}")
    
    // Lombok for code generation
    compileOnly "org.projectlombok:lombok:${Lombok}"
    annotationProcessor "org.projectlombok:lombok:${Lombok}"

    // Commons IO library
    implementation "commons-io:commons-io:${CommonsIO}"

    // Compile-only dependencies
    compileOnly("me.clip:placeholderapi:${PlaceholderAPI}")
    compileOnly("com.github.MilkBowl:VaultAPI:${VaultAPI}")
    compileOnly("com.mojang:authlib:${AuthLib}")
    compileOnly("org.spigotmc:spigot-api:${SpigotAPI}")
    compileOnly("com.sk89q.worldedit:worldedit-bukkit:${WorldEdit}")
    compileOnly("com.sk89q.worldedit:worldedit-core:${WorldEdit}")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:${FAWE}")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core:${FAWE}")

    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:${Jupiter}"
    testImplementation "org.mockito:mockito-core:${Mockito}"
    testImplementation "org.assertj:assertj-core:${Assertj}"
    testImplementation "org.projectlombok:lombok:${Lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${Lombok}"
}

processResources {
    expand project.properties  // Replace placeholders in resources
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'  // Ensure plugin.yml is included
        duplicatesStrategy DuplicatesStrategy.INCLUDE  // Handle duplicates
    }
    filesMatching('plugin.yml') {
        expand (
            pluginname: project.pluginname,
            group: project.group,
            version: project.version,
            authors: project.authors,
            description: project.description,
            apiversion: project.apiversion,
            depend: project.depend,
            softdepend: project.softdepend,
        )
    }
}

shadowJar {
    minimize()
    archiveClassifier.set('')
    archiveFileName.set("${project.pluginname}-${project.version}.jar")
}

javadoc {
    options.links(
            "https://javadoc.io/static/org.jetbrains/annotations/20.1.0/",
            "https://docs.oracle.com/javase/21/docs/api/",
            "https://papermc.io/javadocs/paper/${project.minecraft}/"
    )
    source = sourceSets.main.allJava
    include("**/api/*")
    destinationDir = new File("build/javadocs")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

publishing {
    publications {
        customLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "${project.pluginname}"
            url = layout.buildDirectory.dir("repo")
        }
    }
}

tasks.build.dependsOn tasks.shadowJar

defaultTasks 'build'
